plugins {
    id("java")
    id("com.github.johnrengelman.shadow") version("7.0.0")
}

group(groupName)
version(versionString)

allprojects {
    repositories {
        mavenCentral()

        maven { url("https://repo.unnamed.team/repository/unnamed-releases/") }
        maven { url("https://repo.unnamed.team/repository/unnamed-snapshots/") }

        maven { url("https://oss.sonatype.org/content/repositories/snapshots/") }
        maven { url("https://hub.spigotmc.org/nexus/content/repositories/snapshots/") }
    }
}

shadowJar {
    archiveFileName.set("${rootProjectName}-${name} v${versionString}.jar")

    relocate("org.jetbrains", "${libraryPackage}.annotations");
    relocate("team.unnamed.gui", "${libraryPackage}.gui")
    relocate("com.zaxxer", "${libraryPackage}.hikaricp")
    relocate("org.jdbi", "${libraryPackage}.jdbi")
}

dependencies {
    //annotations
    implementation("org.jetbrains:annotations:16.0.2")

    //spigot
    compileOnly("org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT")

    //gui
    implementation("team.unnamed.gui:core:2.3.3-SNAPSHOT")

    //database connectivity (mysql)
    implementation("com.zaxxer:HikariCP:4.0.3")
    implementation("org.jdbi:jdbi3-core:3.1.0")

    //packet compatibility
    implementation(project(":packets"))

    [
        "1_8_8-R0_1",
        "1_12_2-R0_1",
        "1_16_5-R0_1",
        "1_17_1-R0_1"
    ].forEach({
        implementation(project(":packets:${it}"))
    })
}